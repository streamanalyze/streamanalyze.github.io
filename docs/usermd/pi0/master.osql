create type pi0hat:signal under Signal;


create function environphat_stream(pi0hat:signal s) -> Stream of Timeval of Vector of Number
  as select Stream of ts(timeval(ts),value*scale)
       from Vector data, Number ts, Integer port, Vector of Integer signals,
            Number scale, vector of Number value
    where port = options(s)["port"]
      and signals = options(s)["signals"]
      and scale = options(s)["scale"]
      and data in csv:socket_stream("localhost",port)
      and data[2] in signals
      and value = skip(data,2)
      and ts = data[1];
       
     


create pi0hat:signal (name, options)
  instances
  ("accelerometer", {"port": 32345, "signals":[1], "scale": 9.82}),
  ("temperature",   {"port": 32345, "signals":[2], "scale": 1}),
  ("heading",       {"port": 32345, "signals":[3], "scale": 1}),
  ("pressure",      {"port": 32345, "signals":[4], "scale": 1}),
  ("light",         {"port": 32345, "signals":[5], "scale": 1}),
  ("rgb",           {"port": 32345, "signals":[6], "scale": 1});


for each pi0hat:signal s {
  set ts_signal_stream(s) = environphat_stream(s);
};


load_osql("start.osql");